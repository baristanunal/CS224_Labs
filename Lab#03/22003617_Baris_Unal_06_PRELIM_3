
CS224
Lab #03
Section 06
Barış Tan Ünal
22003617
14.03.2022


#############################

# LAB 3 | PRELIMINARY PART 1
# CS224-06 Computer Architecture
# Barış Tan Ünal
# 22003617

# Counting instructions.

#############################



.data
	msgMainAddCountPrompt:		.asciiz "\nThe number ADD instructions in MAIN: "
	msgMainOriCountPrompt:		.asciiz "\nThe number ORI instructions in MAIN: "
	msgMainLwCountPrompt:		.asciiz "\nThe number LW instructions in MAIN: "
	msgFuncAddCountPrompt:		.asciiz "\n\nThe number ADD instructions in FUNCTION: "
	msgFuncOriCountPrompt:		.asciiz "\nThe number ORI instructions in FUNCTION: "
	msgFuncLwCountPrompt:		.asciiz "\nThe number LW instructions in FUNCTION: "

.text

.globl main
main:	

		# 1. Call the function for main.
	BeginL:
		la $a0, BeginL				# ori inside the la instruction!
		la $a1, EndL				
		
		jal instructionCount
		
		add $s0, $v0, $0			# $s0: MAIN ADD count
		add $s1, $v1, $0			# $s1: MAIN ORI count
		add $s2, $a0, $0			# $s2: MAIN LW count
		
		
		# 2. Call the function for the function itself.
		la $a0, instructionCount
		la $a1, EndOfInstCount
		
		jal instructionCount
		
		add $s3, $v0, $0			# $s3: FUNC ADD count
		add $s4, $v1, $0			# $s4: FUNC ORI count
		add $s5, $a0, $0			# $s5: FUNC LW count
		
		
		# 3. Print the results.
		
		# 3.1.1
		li $v0, 4
		la $a0, msgMainAddCountPrompt
		syscall
		
		add $a0, $s0, $0
		li  $v0, 1
		syscall
		
		# 3.1.2
		li $v0, 4
		la $a0, msgMainOriCountPrompt
		syscall
		
		add $a0, $s1, $0
		li  $v0, 1
		syscall
		
		# 3.1.3
		li $v0, 4
		la $a0, msgMainLwCountPrompt
		syscall
		
		add $a0, $s2, $0
		li  $v0, 1
		syscall
		
		# 3.2.1
		li $v0, 4
		la $a0, msgFuncAddCountPrompt
		syscall
		
		add $a0, $s3, $0
		li  $v0, 1
		syscall
		
		# 3.2.2
		li $v0, 4
		la $a0, msgFuncOriCountPrompt
		syscall
		
		add $a0, $s4, $0
		li  $v0, 1
		syscall
		
		# 3.2.3
		li $v0, 4
		la $a0, msgFuncLwCountPrompt
		syscall
		
		add $a0, $s5, $0
		li  $v0, 1
		syscall
				
	EndL:		
		li $v0, 10					# end of main
		syscall
	#------------------------------------------------------------
	
.globl instructionCount
instructionCount:
	# 1. Push $s registers to the stack.
	addi $sp, $sp, -32
	sw   $s0, 0($sp)	
	sw   $s1, 4($sp)
	sw   $s2, 8($sp)
	sw   $s3, 12($sp)
	sw   $s4, 16($sp)	
	sw   $s5, 20($sp)
	sw   $s6, 24($sp)
	sw   $s7, 28($sp)
	
	add $s0, $0, $0					# $s0: add
	add $s1, $0, $0					# $s1: ori
	add $s2, $0, $0					# $s2: lw
	add $s3, $0, $0					# $s3: temp inst
	
	addi $s5, $0, 0x00000020			# $s5: add funct
	addi $s6, $0, 0x0000000d			# $s6: ori opcode
	addi $s7, $0, 0x00000023			# $s7: lw  opcode

	# 2. Traverse over the addresses between given labels.
	FuncLoop:
		lw   $s3, 0($a0)
		andi $s4, $s3, 0x0000003f		# $s4: function field
		srl  $s3, $s3, 26			# $s3: opcode field
		
		# 2.1 Add instruction.
		bne  $s3, $0, SkipAdd
		bne  $s4, $s5, SkipAdd
		addi $s0, $s0, 1
		j FuncTest
					
	SkipAdd:	
		# 2.2 Ori instruction.
		bne  $s3, $s6, SkipOri
		addi $s1, $s1, 1
		j FuncTest
		
	SkipOri:		
		# 2.3 Lw instruction.							
		bne  $s3, $s7, FuncTest
		addi $s2, $s2, 1
		j FuncTest
		
	FuncTest:
		addi $a0, $a0, 4
		bne  $a0, $a1, FuncLoop
		
		add $v0, $s0, $0			# $v0: add count
		add $v1, $s1, $0			# $v1: ori count
		add $a0, $s2, $0			# $a0: lw count
	
	# 3. Pop $s registers from the stack.
	lw   $s0, 0($sp)	
	lw   $s1, 4($sp)
	lw   $s2, 8($sp)
	lw   $s3, 12($sp)
	lw   $s4, 16($sp)	
	lw   $s5, 20($sp)
	lw   $s6, 24($sp)
	lw   $s7, 28($sp)
	addi $sp, $sp, 32
	
EndOfInstCount:
	jr $ra

#-------------------------------------
# End of function section.
#-------------------------------------



#############################

# LAB 3 | PRELIMINARY PART 2
# CS224-06 Computer Architecture
# Barış Tan Ünal
# 22003617

# Recursive division.

#############################


.data
	msgDividentPrompt:		.asciiz "\nPlease enter the divident: "
	msgDivisorPrompt:		.asciiz "\nPlease enter the divisor: "
	msgQuotientPrompt:		.asciiz "\nThe quotient is: "
	msgContinuePrompt:		.asciiz "\n\nDo you want to exit or continue? (0 to exit, 1 to continue.): "

.text

.globl main	
main:

Continue:
	# 1. Ask for the inputs.
	
	# 1.1.1 Ask for the divident.
	li $v0, 4
	la $a0, msgDividentPrompt
	syscall
	
	# 1.1.2 Get the divident.
	li $v0, 5
	syscall
	add $s0, $v0, $0				# $s0: divident 
		
	# 1.2.1 Ask for the divisor.
	li $v0, 4
	la $a0, msgDivisorPrompt
	syscall
	
	# 1.2.1 Get the divisor.
	li $v0, 5
	syscall
	add $s1, $v0, $0				# $s1: divisor 


	# 2. Call the recursiveDivision function.
	add $a0, $s0, $0
	add $a1, $s1, $0
			
	jal recursiveDivision
	
	#----------------------------
	# 3. Perform the sub-program.
	#----------------------------
	
	
	# 4. Display the quotient.
	add $s2, $v0, $0				# $s2: quotient
	
	li $v0, 4
	la $a0, msgQuotientPrompt
	syscall
	
	add $a0, $s2, $0
	li  $v0, 1
	syscall
	
	
	# 5. Ask the user if they want to continue with other inputs.
	li $v0, 4
	la $a0, msgContinuePrompt
	syscall
	
	li $v0, 5
	syscall
	add $t0, $v0, $0				# $t0: continue value (exit if 0, continue if 1) 
	
	bne $t0, $0, Continue
		
	li $v0, 10					# end of main
	syscall

	#------------------------------------------------------------
	
.globl recursiveDivision	
recursiveDivision:
	# 3.1.1 Push $s registers and $ra to the stack.
	addi $sp, $sp, -8
	sw   $ra, 0($sp)	
	sw   $s0, 4($sp)
	
	# 3.1.2 Base case.
	li  $v0, -1
	slt $s1, $a0, $0
	bne $s1, $0, DivisionDone
	
	# 3.1.3 Subtract the divisor from divident once.
	add $s0, $a0, $0
	sub $a0, $a0, $a1
	jal recursiveDivision
		
	# 3.1.4 Add 1 to the return value.	
	addi $v0, $v0, 1
	
	DivisionDone:
		lw   $ra, 0($sp)
		lw   $s0, 4($sp)
		addi $sp, $sp, 8  
		jr $ra	

#-------------------------------------
# End of function section.
#-------------------------------------



